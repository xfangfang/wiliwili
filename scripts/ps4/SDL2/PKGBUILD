# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2
pkgver=2.0.18
pkgrel=16
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
gitcommit=bf797a5315c7dbc0757d8ae7228e2dff1bf370cb
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=('ps4-openorbis-libsamplerate')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/PacBrew/SDL/archive/$gitcommit.zip")
sha256sums=('97b6ed13fb3aa49dfa35737981458c6cecf8baae5fa87ae7216bcb2a09df70a9')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL-$gitcommit

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  # Handle this in our own application
  sed -i s'|sceSystemServiceHideSplashScreen();|//sceSystemServiceHideSplashScreen();|'g src/video/ps4/SDL_ps4video.c

  # Change gamepad mapping
  sed -i s'|out->back.target = 4;|out->back.target = 6;|'g src/joystick/ps4/SDL_sysjoystick.c
  sed -i s'|out->start.target = 6;|out->start.target = 4;|'g src/joystick/ps4/SDL_sysjoystick.c

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make --quiet -j $(getconf _NPROCESSORS_ONLN)
}

package() {
  cd SDL-$gitcommit/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}